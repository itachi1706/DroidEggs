apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

def isGHActions = System.getenv("GITHUB_ACTIONS") ?: false

android {
    compileSdk 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        applicationId "com.itachi1706.droideggs"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 649
        versionName "4.3.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // add this to set a unique tray authority based on your applicationId
        resValue "string", "tray__authority", "${applicationId}.tray"
        resourceConfigurations += ['en']
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    packaging {
        resources.excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE*']
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled true
        }
        googlePlay {
            initWith release
            matchingFallbacks = ['release']
            minifyEnabled true
            shrinkResources true
        }
    }
    // This enables long timeouts required on slow environments, e.g. Travis
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }
    namespace 'com.itachi1706.droideggs'
    installation {
        timeOutInMs 10 * 60 * 1000 // Set the timeout to 10 minutes
        installOptions = ['-d', '-t']
    }
    lint {
        abortOnError !isGHActions
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
}

ext {
    supportLibraryVersion = '28.0.0+'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:33.0.0')
    // Import the Compose BOM
    implementation platform('androidx.compose:compose-bom:2024.06.00')

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.compose.animation:animation-core-android'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.ui:ui-graphics-android'
    implementation 'androidx.compose.foundation:foundation-android:1.6.7'
    implementation 'androidx.compose.ui:ui-tooling-preview-android:1.6.7'
    implementation 'androidx.window:window:1.3.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha03'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.itachi1706.appupdater:appupdater:3.0.2'
    implementation 'com.itachi1706.helpers:helperlib:1.4.3'
    implementation 'me.jfenn:Attribouter:0.1.9'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
